?start: expr

?expr: oop_bool
    | oop_bool "?" oop_add ":" oop_add -> op_ternary

?oop_bool : oop_cond
    | oop_bool "&&" oop_cond    -> op_and
    | oop_bool "||" oop_cond    -> op_or
    | "!" oop_cond              -> op_not

?oop_cond: oop_add
    | oop_add "=" oop_cond      -> op_assign
    | oop_add "==" oop_add      -> op_equals
    | oop_add "!=" oop_add      -> op_differs
    | oop_add "<" oop_add       -> op_less
    | oop_add "<=" oop_add      -> op_less_eq
    | oop_add ">" oop_add       -> op_greater
    | oop_add ">=" oop_add      -> op_greater_eq

?oop_add: oop_mul
    | "-" oop_mul               -> op_neg
    | "+" oop_mul               -> op_plus
    | oop_add "+" oop_mul       -> op_add
    | oop_add "-" oop_mul       -> op_sub

?oop_mul: oop_exp
    | oop_mul "*" oop_exp       -> op_mul
    | oop_mul ".*" oop_exp      -> op_emul
    | oop_mul "/" oop_exp       -> op_div
    | oop_mul "./" oop_exp      -> op_ediv
    | oop_mul "\\" oop_exp      -> op_solve
    | oop_mul "%" oop_exp       -> op_modulo

?oop_exp: value
    | value "^" oop_exp         -> op_pow

?value: "(" expr ")"
    | "|" expr "|"                      -> op_abs
    | value "(" expr? ("," expr)* ")"   -> op_evaluate
    | constant
    | variable

?variable: NAME             -> make_variable

?constant: scalar
    | matrix
    | array

?scalar: /\d+\.?\d*([eE][-+]?\d+)?/             -> form_decimal
    | /\d*\.?\d+([eE][-+]?\d+)?/                -> form_decimal
    | /\d+\.?\d*([eE][-+]?\d+)?j/               -> form_decimalj
    | /\d*\.?\d+([eE][-+]?\d+)?j/               -> form_decimalj

?matrix: "[" matrix_row (";" matrix_row)+ "]"       -> form_matrix

?matrix_row: expr ("," expr)*                       -> form_matrix_row

?array: "[" matrix_row "]"                          -> form_array

?command: "/clear" variable? ("," variable)*        -> clear_vars
    | "/vars"                                       -> show_vars

%import common.CNAME -> NAME
%import common.WS_INLINE
%ignore WS_INLINE

COMMENT: "#" /[^\n]/*
%ignore COMMENT